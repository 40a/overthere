// build.gradle for overthere repository containing the overthere and itest-support projects

buildscript {
	repositories {
		add(new org.apache.ivy.plugins.resolver.URLResolver()) {
			name = "GitHub"
			addArtifactPattern 'http://cloud.github.com/downloads/hierynomus/gradle-plugins/[organization]-[module]-[revision].[ext]'
		}
	}

	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.1-SNAPSHOT@jar'
	}
}

defaultTasks 'build'
apply from: 'https://github.com/xebialabs/deployit-common-build/raw/master/dependencies.gradle'

allprojects {
	version = '1.0-alpha-19-SNAPSHOT'
	group = 'com.xebialabs.overthere'

	apply plugin: 'idea'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'license'

	apply from: 'https://github.com/xebialabs/deployit-common-build/raw/master/repositories.gradle'
	apply from: 'https://github.com/xebialabs/deployit-common-build/raw/master/releaseable.gradle'
	apply from: 'https://github.com/xebialabs/deployit-common-build/raw/master/artifacts.gradle'

	sourceCompatibility = 1.6
	targetCompatibility = 1.6

	configurations {
	    compile {
	        transitive = false
	    }
	}

	test {
		excludes = ["**/*Itest.*"]
	}

	task itest(type: Test) {
		includes = ['**/*Itest.*']
		testResultsDir = file("${buildDir}/itest-results")

		copyProjectPropertyToSystemProperty(project, systemProperties, 'itests')
		copyProjectPropertyToSystemProperty(project, systemProperties, 'aws.endpoint')
		copyProjectPropertyToSystemProperty(project, systemProperties, 'aws.accessKey')
		copyProjectPropertyToSystemProperty(project, systemProperties, 'aws.secretKey')
	}
}

def copyProjectPropertyToSystemProperty(project, systemProperties, name) {
	if(project.hasProperty(name)) {
		systemProperties[name] = project.getProperty(name)
	}
}

